plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'io.github.hamsteak'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.security:spring-security-crypto'

    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.2'
    implementation 'net.ttddyy:datasource-proxy:1.10.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    implementation("io.github.openfeign.querydsl:querydsl-jpa:7.1")
    annotationProcessor("io.github.openfeign.querydsl:querydsl-apt:7.1:jakarta")
    annotationProcessor("jakarta.annotation:jakarta.annotation-api")
    annotationProcessor("jakarta.persistence:jakarta.persistence-api")

    implementation 'commons-validator:commons-validator:1.10.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    addNettyDnsWhenMacArm(delegate);
}

def addNettyDnsWhenMacArm(DependencyHandler dependencyHandler) {
    def os = System.getProperty("os.name").toLowerCase()
    def arch = System.getProperty("os.arch")

    if (os.contains("mac") && (arch.equals("aarch64"))) {
        dependencyHandler.add("runtimeOnly", "io.netty:netty-resolver-dns-native-macos:4.2.9.Final:osx-aarch_64")
    }
}

ext {
    snippetsDir = file('build/generated-snippets')
}

processTestResources {
    // Copy schema file for initializing test db.
    from('db') {
        include 'schema.sql'
    }
}

test {
    outputs.dir snippetsDir
    systemProperty "spring.profiles.active", "test"
    useJUnitPlatform()
}

asciidoctor {
    doFirst {
        delete file('src/main/resources/static/docs')
        println("Delete Docs")
    }

    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test

    baseDirFollowsSourceFile()
}

tasks.register('copyDocument', Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc/")
    into file("src/main/resources/static/docs")
}

bootJar {
    dependsOn copyDocument
    from('src/main/resources/static/docs') {
        into('static/docs')
    }
}

clean {
    delete file('src/main/generated')
}
